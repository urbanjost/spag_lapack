!*==zrzt02.f90  processed by SPAG 7.51RB at 17:35 on  4 Mar 2022
!> \brief \b zrzt02
!
!  =========== DOCUMENTATION ===========
!
! Online html documentation available at
!            http://www.netlib.org/lapack/explore-html/
!
!  Definition:
!  ===========
!
!       DOUBLE PRECISION FUNCTION zrzt02( M, N, AF, LDA, TAU, WORK,
!                        LWORK )
!
!       .. Scalar Arguments ..
!       INTEGER            LDA, LWORK, M, N
!       ..
!       .. Array Arguments ..
!       COMPLEX*16         AF( LDA, * ), TAU( * ), WORK( LWORK )
!       ..
!
!
!> \par Purpose:
!  =============
!>
!> \verbatim
!>
!> zrzt02 returns
!>      || I - Q'*Q || / ( M * eps)
!> where the matrix Q is defined by the Householder transformations
!> generated by ZTZRZF.
!> \endverbatim
!
!  Arguments:
!  ==========
!
!> \param[in] M
!> \verbatim
!>          M is INTEGER
!>          The number of rows of the matrix AF.
!> \endverbatim
!>
!> \param[in] N
!> \verbatim
!>          N is INTEGER
!>          The number of columns of the matrix AF.
!> \endverbatim
!>
!> \param[in] AF
!> \verbatim
!>          AF is COMPLEX*16 array, dimension (LDA,N)
!>          The output of ZTZRZF.
!> \endverbatim
!>
!> \param[in] LDA
!> \verbatim
!>          LDA is INTEGER
!>          The leading dimension of the array AF.
!> \endverbatim
!>
!> \param[in] TAU
!> \verbatim
!>          TAU is COMPLEX*16 array, dimension (M)
!>          Details of the Householder transformations as returned by
!>          ZTZRZF.
!> \endverbatim
!>
!> \param[out] WORK
!> \verbatim
!>          WORK is COMPLEX*16 array, dimension (LWORK)
!> \endverbatim
!>
!> \param[in] LWORK
!> \verbatim
!>          LWORK is INTEGER
!>          Length of WORK array. LWORK >= N*N+N.
!> \endverbatim
!
!  Authors:
!  ========
!
!> \author Univ. of Tennessee
!> \author Univ. of California Berkeley
!> \author Univ. of Colorado Denver
!> \author NAG Ltd.
!
!> \date December 2016
!
!> \ingroup complex16_lin
!
!  =====================================================================
      DOUBLE PRECISION FUNCTION zrzt02(M,N,Af,Lda,Tau,Work,Lwork)
      IMPLICIT NONE
!*--ZRZT0294
!
!  -- LAPACK test routine (version 3.7.0) --
!  -- LAPACK is a software package provided by Univ. of Tennessee,    --
!  -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
!     December 2016
!
!     .. Scalar Arguments ..
      INTEGER Lda , Lwork , M , N
!     ..
!     .. Array Arguments ..
      COMPLEX*16 Af(Lda,*) , Tau(*) , Work(Lwork)
!     ..
!
!  =====================================================================
!
!     .. Parameters ..
      DOUBLE PRECISION ZERO , ONE
      PARAMETER (ZERO=0.0D0,ONE=1.0D0)
!     ..
!     .. Local Scalars ..
      INTEGER i , info
!     ..
!     .. Local Arrays ..
      DOUBLE PRECISION rwork(1)
!     ..
!     .. External Functions ..
      DOUBLE PRECISION DLAMCH , ZLANGE
EXTERNAL    DLAMCH,   ZLANGE
!     ..
!     .. External Subroutines ..
EXTERNAL    ZLASET,   ZUNMRZ
!     ..
!     .. Intrinsic Functions ..
      INTRINSIC DBLE , DCMPLX , MAX
!     ..
!     .. Executable Statements ..
!
      zrzt02 = ZERO
!
      IF ( Lwork<N*N+N ) THEN
         CALL xerbla('zrzt02',7)
         RETURN
      ENDIF
!
!     Quick return if possible
!
      IF ( M<=0 .OR. N<=0 ) RETURN
!
!     Q := I
!
      CALL ZLASET('Full',N,N,DCMPLX(ZERO),DCMPLX(ONE),Work,N)
!
!     Q := P(1) * ... * P(m) * Q
!
      CALL ZUNMRZ('Left','No transpose',N,N,M,N-M,Af,Lda,Tau,Work,N,    &
     &            Work(N*N+1),Lwork-N*N,info)
!
!     Q := P(m)' * ... * P(1)' * Q
!
      CALL ZUNMRZ('Left','Conjugate transpose',N,N,M,N-M,Af,Lda,Tau,    &
     &            Work,N,Work(N*N+1),Lwork-N*N,info)
!
!     Q := Q - I
!
      DO i = 1 , N
         Work((i-1)*N+i) = Work((i-1)*N+i) - ONE
      ENDDO
!
      zrzt02 = ZLANGE('One-norm',N,N,Work,N,rwork)                      &
     &         /(DLAMCH('Epsilon')*DBLE(MAX(M,N)))
!
!     End of zrzt02
!
      END FUNCTION zrzt02
