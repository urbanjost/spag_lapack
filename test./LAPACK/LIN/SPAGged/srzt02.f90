!*==srzt02.f90  processed by SPAG 7.51RB at 20:37 on  3 Mar 2022
!> \brief \b SRZT02
!
!  =========== DOCUMENTATION ===========
!
! Online html documentation available at
!            http://www.netlib.org/lapack/explore-html/
!
!  Definition:
!  ===========
!
!       REAL             FUNCTION SRZT02( M, N, AF, LDA, TAU, WORK,
!                        LWORK )
!
!       .. Scalar Arguments ..
!       INTEGER            LDA, LWORK, M, N
!       ..
!       .. Array Arguments ..
!       REAL               AF( LDA, * ), TAU( * ), WORK( LWORK )
!       ..
!
!
!> \par Purpose:
!  =============
!>
!> \verbatim
!>
!> SRZT02 returns
!>      || I - Q'*Q || / ( M * eps)
!> where the matrix Q is defined by the Householder transformations
!> generated by STZRZF.
!> \endverbatim
!
!  Arguments:
!  ==========
!
!> \param[in] M
!> \verbatim
!>          M is INTEGER
!>          The number of rows of the matrix AF.
!> \endverbatim
!>
!> \param[in] N
!> \verbatim
!>          N is INTEGER
!>          The number of columns of the matrix AF.
!> \endverbatim
!>
!> \param[in] AF
!> \verbatim
!>          AF is REAL array, dimension (LDA,N)
!>          The output of STZRZF.
!> \endverbatim
!>
!> \param[in] LDA
!> \verbatim
!>          LDA is INTEGER
!>          The leading dimension of the array AF.
!> \endverbatim
!>
!> \param[in] TAU
!> \verbatim
!>          TAU is REAL array, dimension (M)
!>          Details of the Householder transformations as returned by
!>          STZRZF.
!> \endverbatim
!>
!> \param[out] WORK
!> \verbatim
!>          WORK is REAL array, dimension (LWORK)
!> \endverbatim
!>
!> \param[in] LWORK
!> \verbatim
!>          LWORK is INTEGER
!>          length of WORK array. LWORK >= N*N+N*NB.
!> \endverbatim
!
!  Authors:
!  ========
!
!> \author Univ. of Tennessee
!> \author Univ. of California Berkeley
!> \author Univ. of Colorado Denver
!> \author NAG Ltd.
!
!> \date December 2016
!
!> \ingroup single_lin
!
!  =====================================================================
      REAL FUNCTION SRZT02(M,N,Af,Lda,Tau,Work,Lwork)
      IMPLICIT NONE
!*--SRZT0294
!
!  -- LAPACK test routine (version 3.7.0) --
!  -- LAPACK is a software package provided by Univ. of Tennessee,    --
!  -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
!     December 2016
!
!     .. Scalar Arguments ..
      INTEGER Lda , Lwork , M , N
!     ..
!     .. Array Arguments ..
      REAL Af(Lda,*) , Tau(*) , Work(Lwork)
!     ..
!
!  =====================================================================
!
!     .. Parameters ..
      REAL ZERO , ONE
      PARAMETER (ZERO=0.0E+0,ONE=1.0E+0)
!     ..
!     .. Local Scalars ..
      INTEGER i , info
!     ..
!     .. Local Arrays ..
      REAL rwork(1)
!     ..
!     .. External Functions ..
      REAL SLAMCH , SLANGE
      EXTERNAL SLAMCH , SLANGE
!     ..
!     .. External Subroutines ..
      EXTERNAL SLASET , SORMRZ , XERBLA
!     ..
!     .. Intrinsic Functions ..
      INTRINSIC MAX , REAL
!     ..
!     .. Executable Statements ..
!
      SRZT02 = ZERO
!
      IF ( Lwork<N*N+N ) THEN
         CALL XERBLA('SRZT02',7)
         RETURN
      ENDIF
!
!     Quick return if possible
!
      IF ( M<=0 .OR. N<=0 ) RETURN
!
!     Q := I
!
      CALL SLASET('Full',N,N,ZERO,ONE,Work,N)
!
!     Q := P(1) * ... * P(m) * Q
!
      CALL SORMRZ('Left','No transpose',N,N,M,N-M,Af,Lda,Tau,Work,N,    &
     &            Work(N*N+1),Lwork-N*N,info)
!
!     Q := P(m) * ... * P(1) * Q
!
      CALL SORMRZ('Left','Transpose',N,N,M,N-M,Af,Lda,Tau,Work,N,       &
     &            Work(N*N+1),Lwork-N*N,info)
!
!     Q := Q - I
!
      DO i = 1 , N
         Work((i-1)*N+i) = Work((i-1)*N+i) - ONE
      ENDDO
!
      SRZT02 = SLANGE('One-norm',N,N,Work,N,rwork)                      &
     &         /(SLAMCH('Epsilon')*REAL(MAX(M,N)))
!
!     End of SRZT02
!
      END FUNCTION SRZT02
